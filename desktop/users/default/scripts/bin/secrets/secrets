#!/usr/bin/env -S just -f
# secrets: Secret management commands using ragenix and rage

USER := env_var("USER")
HOME := env_var("HOME")

export JUST_JUSTFILE := justfile()
export RULES := HOME + "/nix/desktop/users/" + USER + "/secrets/secrets.nix"

# List all commands
default:
    @just --list

# List all secrets
list:
    #!/usr/bin/env fish
    cd ~/nix/desktop/users/{{USER}}/secrets
    ls -1 *.age

# Edit a secret file
edit secret:
    #!/usr/bin/env fish
    cd ~/nix/desktop/users/{{USER}}/secrets
    ragenix -i ~/.ssh/id_machine -e {{secret}}

# Read a secret file
read secret:
    #!/usr/bin/env fish
    cd ~/nix/desktop/users/{{USER}}/secrets
    rage -i ~/.ssh/id_machine -o /dev/stdout -d {{secret}}

# Write a secret from stdin
write secret:
    #!/usr/bin/env fish
    cd ~/nix/desktop/users/{{USER}}/secrets

    set IFS ""
    set SECRET (cat)

    if test -z $SECRET
        echo "error: no secret provided" >&2
        exit 1
    end

    echo $SECRET | ragenix -i ~/.ssh/id_machine --editor _secrets-tee -e {{secret}}

# Refresh user secrets
refresh:
    #!/usr/bin/env fish
    cd ~/nix/desktop/users/{{USER}}/nix-{{USER}}
    cat example-{{USER}}.yaml | just write example.yaml.age

# Rekey all secrets in user's secrets directory
rekey:
    #!/usr/bin/env fish
    cd ~/nix/desktop/users/{{USER}}/secrets
    ragenix -i ~/.ssh/id_machine --rekey
